production encapsulation
    alternatives
        match @[tok]"public"
        match @[tok]"local"
        match @[tok]"derived"
        match @[tok]"private"

production namespace
    while match attribute
        match @[attributes]attribute
    match "namespace" @[name]ident-navigation
    match "{"
    while not match "}"
        alternatives
            match @[usings]using
            match @[pragmas]pragma
            match @[aliases]alias-encapsulated
            match @[classes]class
            match @[constants]constant
            match @[contracts]contract
            match @[conversions]conversion
            match @[fields]field
            match @[methods]method
            match @[namespaces]namespace
            match @[properties]property
            error
    match "}"

production class-extends
    match ":"
    match @[types]type-navigation
    while match ","
        alternatives
            match "," @[types]type-navigation
            match ","

production class-implements
    match "implementing"
    match @[types]type-navigation
    while match ","
        alternatives
            match "," @[types]type-navigation
            match ","

production class-imports
    match "importing"
    match @[types]type-navigation
    while match ","
        alternatives
            match "," @[types]type-navigation
            match ","
    
production class
    while match attribute
        match @[attributes]attribute
    match @[scope]encapsulation "class" @[name]ident
    if match template-definition
        match @[template]template-definition
    if match class-extends
        match @[extends]class-extends
    if match class-implements
        match @[implements]class-implements
    if match class-imports
        match @[imports]class-imports
    match "{"
    while not match "}"
        alternatives
            match @[usings]using
            match @[pragmas]pragma
            match @[aliases]alias-encapsulated
            match @[classes]class
            match @[constants]constant
            match @[constructors]constructor
            match @[contracts]contract
            match @[copystructors]copystructor
            match @[conversions]conversion
            match @[destructors]destructor
            match @[fields]field
            match @[indexers]indexer
            match @[methods]method
            match @[namespaces]namespace
            match @[operators-binary]operator-binary-overload
            match @[operators-nullar]operator-nullar-overload
            match @[operators-unary]operator-unary-overload
            match @[properties]property
            error
    match "}"

production method-arglist-item
    while match attribute
        match @[attributes]attribute
    match @[type]type-match @[name]ident

production method-arglist
    match "("
    if match method-arglist-item
        match @[args]method-arglist-item
        while not match ")" 
            alternatives
                match "," @[args]method-arglist-item
                error
        if match ","
            match ","
    match ")"
    
production method-arglist-indexer
    match "["
    match @[args]method-arglist-item
    while not match "]"
        alternatives
            match "," @[args]method-arglist-item
            error
    if match ","
        match ","
    match "]"

production method-meta
    match @[encaps]encapsulation
    if match "sealed"
        match "sealed"
        set capture is-sealed true
    if match "virtual"
        match "virtual"
        set capture is-virtual true
    if match "unbound"
        match "unbound"
        set capture is-static true
    if match "inline"
        match "inline"
        set capture is-inline true
    
    
production method
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta @[return-type]type-match @[name]ident
    if match template-definition
        match @[template]template-definition
    match @[arglist]method-arglist @[body]scope

production constructor
    while match attribute
        match @[attributes]attribute
    match @[encaps]encapsulation 
    if match template-definition
        match @[template]template-definition
    match @[arglist]method-arglist @[body]scope

production destructor
    while match attribute
        match @[attributes]attribute
    match @[encaps]encapsulation "~" 
    if match template-definition
        match @[template]template-definition
    match @[arglist]method-arglist @[body]scope

production copystructor
    while match attribute
        match @[attributes]attribute
    match @[encaps]encapsulation "+" 
    if match template-definition
        match @[template]template-definition
    match @[arglist]method-arglist
    alternatives
        match @[body]scope
        match "=" "delete"

production conversion
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta "conversion" @[target-type]type-match
    match "(" @[source-type]type-match @[name]ident ")"
    match @[body]scope

production indexer
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta @[target-type]type-match
    if match template-definition
        match @[template]template-definition
    match @[arglist]method-arglist-indexer @[body]scope-getset
    
production operator-binary-overload
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta @[target-type]type-match  "operator"
    if match template-definition
        match @[template]template-definition
    match "(" @[left]method-arglist-item ")"
    alternatives
        match @[operator]"+"
        match @[operator]"-"
        match @[operator]"*"
        match @[operator]"/"
        match @[operator]"<<<"
        match @[operator]"<<"
        match @[operator]"<="
        match @[operator]"<"
        match @[operator]">>>"
        match @[operator]">>"
        match @[operator]">="
        match @[operator]">"
        match @[operator]"||"
        match @[operator]"|"
        match @[operator]"&&"
        match @[operator]"&"
        match @[operator]"==="
        match @[operator]"=="
        match @[operator]"!="
        match @[operator]"^"
    match "(" @[right]method-arglist-item ")"
    match @[body]scope

production operator-unary-overload
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta @[target-type]type-match  "operator"
    if match template-definition
        match @[template]template-definition
    alternatives
        match @[operator]"+"
        match @[operator]"-"
        match @[operator]"!"
        match @[operator]"~"
    match "(" @[right]method-arglist-item ")"
    match @[body]scope

production operator-nullar-overload
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta "operator"
    if match template-definition
        match @[template]template-definition
    alternatives
        match @[operator]"."
        match @[operator]"->"
    match @[body]scope

production property
    while match attribute
        match @[attributes]attribute
    match @[meta]method-meta @[target-type]type-match @[name]ident @[body]scope-getset

production field
    while match attribute
        match @[attributes]attribute
    match @[encaps]encapsulation
    if match "unbound"
        match "unbound"
        set capture is-static true
    match @[target-type]type-match @[name]ident ";"

production constant
    match "const" @[target-type]type-match @[name]ident "=" @[value]exp-or ";"

production using
    match "using"
    alternatives
        match "namespace" @[ident]ident-navigation
            set capture is-namespace true
        match "class" @[ident]ident-navigation
            set capture is-class true
    if match "="
        match "=" @[alias]ident

production alias-encapsulated
    match @[encaps]encapsulation @[actual]alias

production alias
    match "alias" @[alias]ident "=" @[ident]type-match